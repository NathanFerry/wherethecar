plugins {
    id 'java'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'idea'
    id 'application'
    id 'org.beryx.jlink' version '3.2.0'
}

group = 'groupe1.il3'
version = '0.1.0'

repositories {
    mavenCentral()
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

application {
    mainModule = 'wherethecar.main'
    mainClass = 'groupe1.il3.Launcher'

    applicationDefaultJvmArgs = ["--enable-native-access=javafx.graphics"]
}

javafx {
    version = "25"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Wherethecar'
    }

    // Defining the JAVA_HOME path here is a BAD PRACTICE.
    javaHome = System.getenv("JAVA_HOME") ?: "/usr/lib/jvm/java-25-openjdk-amd64"

    jpackage {
        vendor = 'Groupe1 IL3'
        description = 'Vehicles management app'

        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            print("Building for Windows")
            icon = 'src/main/resources/app/icon.ico'
            installerType = 'exe'
            installerOptions = [
                    '--win-dir-chooser',
                    '--win-shortcut',
                    '--win-menu'
            ]
        }

        if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
            print("Building for Linux")
            icon = 'src/main/resources/app/icon.png'
            installerType = 'deb'
            installerOptions = [
                    '--linux-shortcut',
                    '--linux-menu-group', 'Utility',
                    '--linux-app-category', 'Utility'
            ]
        }
    }
}

dependencies {
    implementation 'org.postgresql:postgresql:42.7.8'

    testImplementation platform('org.junit:junit-bom:6.0.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}